##########################################################

var

   e1A, e2A, mu1A, mu2A,
   peeA, pemuA, pmueA, pmumuA,
   NeeA, NemuA, NmueA, NmumuA,
   NtotA,
   pA[4],
   NA[4],

   e1B, e2B, mu1B, mu2B,
   peeB, pemuB, pmueB, pmumuB,
   NeeB, NemuB, NmueB, NmumuB,
   NtotB,
   pB[4],
   NB[4],
   
   p[8],
   N[8],
   Ntot,

   Ree, Rmumu, 

   NDYeeA, NDYmumuA,
   NDYeeB, NDYmumuB
 

##########################################################

data {

 NtotA <- NeeA + NemuA + NmueA + NmumuA
 NtotB <- NeeB + NemuB + NmueB + NmumuB
 Ntot  <- NtotA + NtotB

 NA[1] <- NeeA
 NA[2] <- NemuA
 NA[3] <- NmueA
 NA[4] <- NmumuA

 NB[1] <- NeeB
 NB[2] <- NemuB
 NB[3] <- NmueB
 NB[4] <- NmumuB


 N[1] <- NeeA
 N[2] <- NemuA
 N[3] <- NmueA
 N[4] <- NmumuA
 N[5] <- NeeB
 N[6] <- NemuB
 N[7] <- NmueB
 N[8] <- NmumuB

}

##########################################################

model
{

 ###############
 #### model ####
 ###############

 ###########################################
 ##### Multinomial: ee, emu, mue, mumu #####
 ###########################################

 ##################################
 ##### Probability definition #####
 ##################################

 ################
 #### A zone ####
 ################

 peeA   <- (0.5 * DYAInit * e1A * e2A + 0.25 * OtherAInit * e1A * e2A) / NtotA
 pemuA  <- 0.25 * OtherAInit * e1A * mu2A / NtotA
 pmueA  <- 0.25 * OtherAInit * mu1A * e2A / NtotA
 pmumuA <- (0.5 * DYAInit * mu1A * mu2A + 0.25 * OtherAInit * mu1A * mu2A) / NtotA

 pA[1] <- peeA
 pA[2] <- pemuA
 pA[3] <- pmueA
 pA[4] <- pmumuA


 ################
 #### B zone ####
 ################

 peeB   <- (0.5 * DYBInit * e1B * e2B + 0.25 * OtherBInit * e1B * e2B) / NtotB
 pemuB  <- 0.25 * OtherBInit * e1B * mu2B / NtotB
 pmueB  <- 0.25 * OtherBInit * mu1B * e2B / NtotB
 pmumuB <- (0.5 * DYBInit * mu1B * mu2B + 0.25 * OtherBInit * mu1B * mu2B) / NtotB

 pB[1] <- peeB
 pB[2] <- pemuB
 pB[3] <- pmueB
 pB[4] <- pmumuB


 #########################
 #### multinomial A+B ####
 #########################

 totP <- pA[1] + pA[2] + pA[3] + pA[4] + pB[1] + pB[2] + pB[3] + pB[4]
 
 p[1] <- pA[1] / totP
 p[2] <- pA[2] / totP
 p[3] <- pA[3] / totP
 p[4] <- pA[4] / totP
 p[5] <- pB[1] / totP
 p[6] <- pB[2] / totP
 p[7] <- pB[3] / totP
 p[8] <- pB[4] / totP

 N ~ dmulti (p, Ntot)


 ##########
 ### ee ###

 NDYeeA <-  p[1] / (0.5 * DYAInit * e1A * e2A + 0.25 * OtherAInit * e1A * e2A) * (0.5 * DYAInit * e1A * e2A) * Ntot
 NDYeeB <-  p[5] / (0.5 * DYBInit * e1B * e2B + 0.25 * OtherBInit * e1B * e2B) * (0.5 * DYBInit * e1B * e2B) * Ntot

 Ree <- NDYeeA / NDYeeB 
 ReeSimple <- (NeeA - sqrt(NeeA / NmumuA * NemuA * NmueA) ) / (NeeB - sqrt(NeeB / NmumuB * NemuB * NmueB))
 
 ############
 ### mumu ###

 NDYmumuA <-  p[4] / (0.5 * DYAInit * mu1A * mu2A + 0.25 * OtherAInit * mu1A * mu2A) * (0.5 * DYAInit * mu1A * mu2A) * Ntot
 NDYmumuB <-  p[8] / (0.5 * DYBInit * mu1B * mu2B + 0.25 * OtherBInit * mu1B * mu2B) * (0.5 * DYBInit * mu1B * mu2B) * Ntot

 Rmumu <- ( NDYmumuA ) / ( NDYmumuB )
 RmumuSimple   <- (NmumuA - sqrt(NmumuA / NeeA * NmueA * NemuA) ) / (NmumuB - sqrt(NmumuB / NeeB * NmueB * NemuB)) 
  



 ################
 #### priors ####
 ################

 tempNtotAInit ~ dunif (1,100000)
 NtotAInit <- round (tempNtotAInit)
 tempOtherAInit ~ dunif (1,10000)
 OtherAInit <- round (tempOtherAInit)
 DYAInit <- NtotAInit - OtherAInit

 tempNtotBInit ~ dunif (1,100000)
 NtotBInit <- round (tempNtotBInit)
 tempOtherBInit ~ dunif (1,10000)
 OtherBInit <- round (tempOtherBInit)
 DYBInit <- NtotBInit - OtherBInit

 
 e1A  ~ dunif(0,1)
 e2A  ~ dunif(0,1)
 mu1A ~ dunif(0,1)
 mu2A ~ dunif(0,1)

 e1B  ~ dunif(0,1)
 e2B  ~ dunif(0,1)
 mu1B ~ dunif(0,1)
 mu2B ~ dunif(0,1)

}







